buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath "se.transmode.gradle:gradle-docker:1.2"
    }
}

apply plugin: 'docker'

allprojects {
    group 'micronautsamples'
    version '0.9.BUILD-SNAPSHOT'
}

task copyClientResources(dependsOn: ':music-client:build') {
    group = 'build'
    description = 'Copy client resources into server'
}

copyClientResources.doLast {
    copy {
        from "${project(':music-client').projectDir}/public"
        into "${project(':music-service').buildDir}/resources/main/public/"
    }
}

task assembleServerAndClient(dependsOn: ['copyClientResources', ':music-service:shadowJar']) {
    group = 'build'
    description = 'Build combined server & client JAR'
}

task buildDocker(type: Docker) {
    baseImage = 'adoptopenjdk/openjdk11-openj9:jdk-11.0.1.13-alpine-slim'
    maintainer = 'Jeff Scott Brown <brownj@objectcomputing.com>'
    push = project.hasProperty('pushDocker')
    addFile {
        from project('music-service').shadowJar
        rename {'music-search.jar'}
    }
    exposePort 8080
    entryPoint(['java', '-XX:+UnlockExperimentalVMOptions', '-XX:+UseCGroupMemoryLimitForHeap', '-Dcom.sun.management.jmxremote', '-noverify', '-jar', '/music-search.jar'])
}

buildDocker.dependsOn 'assembleServerAndClient'
